buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
		}
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

version = getVersion("VERSION")
def llibrary_version = getVersion("LLIBRARY_VERSION")
group = "com.bobmowzie"
archivesBaseName = "mowziesmobs"
sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    version = "1.12-14.21.0.2375"
    runDir = "run"
	mappings = "snapshot_20170627"
}

repositories {
    mavenCentral()
    maven {
		name = "mcmoddev"
		url = "http://maven.mcmoddev.com"
	}
}

dependencies {
    compile "net.ilexiconn:llibrary:$llibrary_version-1.12:dev"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "llibrary_version", llibrary_version

    from (sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version, "llibrary_version": llibrary_version
    }
        
    from (sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

String getVersion(String type) {
    String major = "0";
    String revision = "0";
    String patch = "0";
    String prefix = "public static final String $type = \"";
    File file = file("src/main/java/com/bobmowzie/mowziesmobs/MowziesMobs.java")
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            String[] pts = s.split("\\.");

            major = pts[0];
            revision = pts[1];
            patch = pts[2];
        }
    }

    return "$major.$revision.$patch";
}